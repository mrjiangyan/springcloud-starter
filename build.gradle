allprojects {
    buildscript {
        ext {
            commonLang3Version = '3.10'
            aspectjVersion = 'deploy.sh.5'
            springBootVersion = '2.7.0'
            springVersion = '5.3.20'
            springCloudVersion = '3.1.2'
            lombokVersion = '1.18.24'
            nacosStarterVersion = '2021.1'
            springfoxVersion = '3.0.0'
            commonEntityVersion = '2023.2.28.2'
            commonVersion = '2023.3.3.1'
            commonPoolVersion = '1.6'
            mybatisVersion = '3.0.1'
            druidVersion = '1.2.11'
            mybatisplusVersion = '3.5.3.1'
            dynamicVersion = '3.5.2'
            tkVersion = '4.1.5'
            oneVersion = '1.8.6.Final'
            reactorVersion = '3.4.24'
            feignVersion = "10.10.1"
            openFeignVersion = '3.1.2'
            validationVersion = '2.0.1.Final'
            mysqlVerision = '8.0.30'
            revision = '2023.3.16.1'
            jacksonVersion = '2.11.1'
            servletVersion = '4.0.1'
            fastjsonVersion = '2.0.19'
            mapstructVersion = '1.5.3.Final'
            knife4jVersion = '3.0.2'
            hibernateVersion = '6.1.6.Final'
            hutoolVersion = '5.8.15'
        }
        repositories {
            mavenLocal()
            mavenCentral()
            maven {
                url = uri('https://s01.oss.sonatype.org/content/repositories/releases/')
            }
        }

    }

}


/**所有子项目应用Java插件*/

subprojects {

    if("$project.name" == 'touchbiz-dependencies'){
        print "project name is $project.name \n"
        return
    }

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    sourceCompatibility = '17'
    group = 'tech.touchbiz.starter'
    version = "${ revision }"

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url = uri('https://s01.oss.sonatype.org/content/repositories/releases/')
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }



    //打包源代码
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    configurations.all {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-tomcat"
        exclude group: "io.undertow", module: "undertow-websockets-jsr"
        exclude group: "io.springfox", module:"springfox-swagger-ui"
    }

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    artifacts {
        archives sourcesJar, javadocJar
    }

    dependencies {
        compileOnly group: 'org.projectlombok', name: 'lombok', version: "${ lombokVersion }"
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: "${ lombokVersion }"
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId = "$project.group"
                artifactId = "$artifactId"
                version = "$project.version"
                from components.java
                artifact sourcesJar
                artifact javadocJar

                // 添加 pom 相关信息
                // https://docs.gradle.org/current/dsl/org.gradle.api.publish.maven.MavenPublication.html
                pom {
                    name = "tech.touchbiz.starter"
                    description = "Spring Cloud 启动包，基于Spring Cloud Alibaba再结合业务做了封装"
                    url = "https://github.com/mrjiangyan/springcloud-starter"
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        // 添加开发者描述，这个id不知道是什么
                        developer {
                            id = "mrjiangyan"
                            name = "steven jiang"
                            email = "mrjiangyan@hotmail.com"
                        }
                    }
                    // 添加你的 git 仓库 信息
                    scm {
                        connection = "scm:git:https://github.com/mrjiangyan/springcloud-starter.git"
                        developerConnection = "scm:git:https://github.com/mrjiangyan/springcloud-starter.git"
                        url = "https://github.com/mrjiangyan/springcloud-starter"
                    }
                }
            }
        }
        repositories {
            maven {
                name 'sonatype'
                url 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                credentials {
                    username = "${NEXUS_USERNAME}"
                    password = "${NEXUS_PASSWORD}"
                }
            }
            maven {
                name = 'sonatypeSnapshot'
                url = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                credentials {
                    username = "${NEXUS_USERNAME}"
                    password = "${NEXUS_PASSWORD}"
                }
            }
        }
    }

    // 签名配置，注意这里的顺序，今天第一次知道 gradle 中的 task 等配置也是有顺序的
    // 必须在 publishing 配置之后
    signing {
        sign publishing.publications.maven
    }
}

